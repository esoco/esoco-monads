plugins {
    id "java-library"
    id "pl.allegro.tech.build.axion-release" version "$axionReleaseVersion"
    id "com.vanniktech.maven.publish" version "$mavenPublishVersion"
}

repositories {
	mavenLocal()
	mavenCentral()
}

scmVersion { tag { prefix.set("") }}

project.version = scmVersion.version
project.group = 'de.esoco'
project.description = 'Functor and monad implementations'

sourceCompatibility = 1.8

dependencies {
	api 'de.esoco:esoco-common:1.5.+'
	
	testImplementation platform("org.junit:junit-bom:$junitVersion")
	testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
	useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

tasks.withType(Javadoc) {
	failOnError = false
	options.addBooleanOption('quiet', true)
	
	if (JavaVersion.current().isJava8Compatible()) {
		options.addBooleanOption('Xdoclint:none', true)
	}
}

mavenPublishing {
  coordinates("$project.group", "$project.name", "$project.version")

  pom {
    name = "$project.name"
    description = "$project.description"
    inceptionYear = "2020"
    url = "https://github.com/esoco/${project.name}"
    licenses {
      license {
        name = "The Apache License, Version 2.0"
        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }
    }
    developers {
      developer {
        id = "esoco"
        name = "esoco GmbH"
        url = "https://esoco.de/"
      }
    }
    scm {
      url = "https://github.com/esoco/${project.name}/"
      connection = "scm:git:git://github.com/esoco/${project.name}.git"
      developerConnection = "scm:git:ssh://git@github.com/esoco/${project.name}.git"
    }
  }
}